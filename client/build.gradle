buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1+', changing: true
    }
}
plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

repositories {
    maven {
        url "https://cursemaven.com"
    }

    flatDir {
        dirs 'libs/ui'
    }
}


apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'maven-publish'


sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: 'stable', version: '39-1.12'
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }

        server {
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }
    }
}

configurations {
    shade
    implementation.extendsFrom shade
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2860'

    shade project(':common')

    //compileOnly fg.deobf("curse.maven:acsguis-541347:4756627")

    compileOnly files('../libs/ui/ACsGuis-1.2.14-dev3-all.jar')
   // compileOnly fileTree(dir: 'libs/ui', include: ['*.jar'])

    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'
}

jar {
    archiveClassifier = 'slim'
    manifest {
        attributes([
                "Specification-Title"     : "gtwhouses",
                "Specification-Vendor"    : "grandtheftwarzonehouses",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "edgarsilva",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

    baseName 'GTWHousesUINoDeps'
}


shadowJar {
    baseName = 'GTWHousesUI'

    archiveClassifier.set('')
    configurations = [project.configurations.shade]
    //Relocate external dependencies
    finalizedBy('reobfShadowJar')
}

assemble.dependsOn shadowJar

//jar.finalizedBy('reobfJar')

reobf {
    shadowJar { }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}

